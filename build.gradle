plugins {
    id 'org.owasp.dependencycheck' version '8.0.1'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

ext.libVersions = [
        junit: '5.9.1',
        spring: '3.3.4',
        mockito: '4.3.1',
]

allprojects {
    group = 'app.quickcase.spring'
    version = '1.3.0'
}

dependencyCheck {
    failBuildOnCVSS = 0
    suppressionFile = 'dependency-check-suppressions.xml'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'

    sourceCompatibility = 17
    targetCompatibility = 17

    repositories {
        mavenCentral()
    }

    dependencies {
        // Spring
        api "org.springframework.boot:spring-boot-starter-security:${libVersions['spring']}"
        api "org.springframework.boot:spring-boot-starter-oauth2-resource-server:${libVersions['spring']}"
        testImplementation "org.springframework.boot:spring-boot-starter-web:${libVersions['spring']}"
        testImplementation "org.springframework.boot:spring-boot-starter-test:${libVersions['spring']}"

        // Jackson
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.0'

        // Transitive dependency of Spring Boot afflicted by CVE-2022-25857
        // TODO remove explicit declaration once Spring Boot upgrade to 3.0.0 or greater
        implementation 'org.yaml:snakeyaml:1.33'

        // JUnit
        testImplementation "org.junit.jupiter:junit-jupiter-api:${libVersions['junit']}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${libVersions['junit']}"

        // Hamcrest
        testImplementation "org.hamcrest:hamcrest:3.0"

        // Mockito
        testImplementation "org.mockito:mockito-core:${libVersions['mockito']}"
        testImplementation "org.mockito:mockito-junit-jupiter:${libVersions['mockito']}"
    }

    tasks.withType(JavaCompile).configureEach {
        // Explicitly required since Spring 6.1 for mapping of application properties
        // https://github.com/spring-projects/spring-framework/wiki/Upgrading-to-Spring-Framework-6.x#parameter-name-retention
        options.compilerArgs.add("-parameters")
    }

    test {
        useJUnitPlatform()

        dependsOn cleanTest
        finalizedBy jacocoTestReport

        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            html.required = true
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.9
                }
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    url.set("https://github.com/quickcase/spring-oidc.git")
                }
            }
        }

        repositories {
            maven {
                name = "Github"
                url = uri("https://maven.pkg.github.com/quickcase/spring-oidc")
                credentials {
                    username = System.getenv("GH_USERNAME")
                    password = System.getenv("GH_TOKEN")
                }
            }
        }
    }
}
